apply plugin: 'java'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
if (!hasProperty('mainClass')) {
    ext.mainClass = 'eu.mihosoft.vrl.fxscad.JFXScad'
}

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
    
    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.5.1'
    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.5.1', classifier: 'sources'
    

    compile group: 'org.codehaus.groovy', name: 'groovy', version:'2.3.7';
    compile group: 'org.codehaus.groovy', name: 'groovy', version:'2.3.7', classifier: 'sources';
    compile group: 'org.codehaus.groovy', name: 'groovy', version:'2.3.7', classifier: 'javadoc';
    
    compile group: 'org.controlsfx', name: 'controlsfx', version: '8.0.6'
    compile group: 'org.controlsfx', name: 'controlsfx', version: '8.0.6', classifier: 'sources'
    
    compile 'commons-io:commons-io:2.4'
    	
    compile project(':JCSG')
    compile project(':java-bowler')
    compile fileTree (dir: 'libs/', includes: ['*.jar'])
}

// create a fat-jar (class files plus dependencies
// excludes VRL.jar (plugin jar files must not start with 'vrl-\\d+')
jar {

    // dependencies except VRL
    from configurations.runtime.asFileTree.
        filter({file->return !file.name.startsWith("vrl-0")}).
        files.collect { zipTree(it) }

    // project class files compiled from source
    from files(sourceSets.main.output.classesDir)
    
    manifest { attributes 'Main-Class': mainClass }
}

task run(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = mainClass;
    //    jvmArgs = ['-Dprism.forceGPU=true', '-Dprism.verbose=true']
    jvmArgs = ['-Dprism.forceGPU=true']
}
